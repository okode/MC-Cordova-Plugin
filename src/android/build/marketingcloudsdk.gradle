buildscript {
    repositories {
        maven { url "https://maven.google.com" }
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'com.google.gms:google-services:4.1.0'
    }
}


repositories {
    maven { url "https://maven.google.com" }
    maven { url "http://salesforce-marketingcloud.github.io/JB4A-SDK-Android/repository" }
}

dependencies {
    implementation ("com.salesforce.marketingcloud:marketingcloudsdk:6.1.+") {
        // At time of release Cordova was not compatible with Android-28.
        // In order to use the 6.1.x SDK with Cordova we needed to exclude support
        // and manually pull in the Android-27 version.
        exclude group: 'com.android.support', module: 'support-v4'
    }
    implementation 'com.android.support:support-v4:27.1.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'
}

cdvPluginPostBuildExtras.push({
    // we use "AfterEvaluate" to check Gradle plugins when other plugins have already run its own 'postBuildExtras' hook
    afterEvaluate {
        def isGServicesPluginAlreadyApplied = false
        def plugins = getPlugins()
        if (plugins != null) {
            isGServicesPluginAlreadyApplied = plugins.find { plugin ->
                return plugin.toString().contains("com.google.gms.googleservices.GoogleServicesPlugin")
            }
        }
        if (!isGServicesPluginAlreadyApplied) {
            apply plugin: com.google.gms.googleservices.GoogleServicesPlugin
        }
    }
})